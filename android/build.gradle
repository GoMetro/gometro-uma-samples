buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.0.1"
        classpath "com.github.dcendents:android-maven-gradle-plugin:2.1"
    }
}

apply plugin: "com.android.application"
apply plugin: "maven-publish"

group = "com.gometroapp.uma.sample"
version = "3.0.0-SNAPSHOT"

def localProperties = new Properties()
localProperties.load(project.rootProject.file("local.properties").newDataInputStream())

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        buildConfigField "String", "GOMETRO_UMA_USERNAME", "\"${localProperties.get("gometro.uma.username")}\""
        buildConfigField "String", "GOMETRO_UMA_PASSWORD", "\"${localProperties.get("gometro.uma.password")}\""

        applicationId "com.gometroapp.uma.sample"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 3003
        versionName = "3.0.3-SNAPSHOT"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        config {
            keyAlias localProperties.get("signingConfigs.config.keyAlias")
            keyPassword localProperties.get("signingConfigs.config.keyPassword")
            storeFile file(localProperties.get("signingConfigs.config.storeFile"))
            storePassword localProperties.get("signingConfigs.config.storePassword")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.config
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    google()
    jcenter()
    mavenLocal()
    maven { url "https://developer.gometroapp.com/nexus/repository/maven-public" }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.1"

    implementation "com.gometroapp.uma:gometro-uma-android:3.0.3"
    implementation "com.gometroapp.tracking:gometro-tracking-android:1.0.19"
    implementation "com.surveymonkey:surveymonkey-android-sdk:2.0.0"

    testImplementation "junit:junit:4.12"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
}

afterEvaluate {
    publishing {
        publications {
            android.applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    if (variant.name == "release") {
                        create("release", MavenPublication) {
                            groupId project.group
                            artifactId "gometro-uma-sample"
                            version project.version

                            artifact(output.outputFile)
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username System.getenv("NEXUS_USERNAME")
                    password System.getenv("NEXUS_PASSWORD")
                }
                url "https://developer.gometroapp.com/nexus/repository/maven-public"
            }
        }
    }
}
